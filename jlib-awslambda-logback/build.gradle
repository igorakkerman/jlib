/*
 * jlib - Open Source Java Library
 *
 *     www.jlib.org
 *
 *
 *     Copyright 2005-2018 Igor Akkerman
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

plugins {
    id 'java'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.7.0'
}
group = 'org.jlib'
version = '1.0.0-SNAPSHOT'
description = 'jlib AWS Lambda Logback Adapter'

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.0'

    implementation ('ch.qos.logback:logback-classic:1.3.0-alpha4') {
        exclude group: 'com.sun.mail', module: 'javax.mail'
    }
    // log4j-over-slf4j: AWS Lambda runtime loads org.apache.log4j.MDC to pass AWSRequestId
    runtimeOnly 'org.slf4j:log4j-over-slf4j:1.8.0-beta2'

    // test
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'

    testImplementation 'org.assertj:assertj-core:3.10.0'

    testImplementation 'org.slf4j:log4j-over-slf4j:1.8.0-beta2'
}

lombok {
    version = "1.18.0"
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            pom {
                url = 'https://www.jlib.org/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Igor Akkerman'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jlib-framework/jlib-awslambda-logback.git'
                    url = 'https://github.com/jlib-framework/jlib-awslambda-logback'
                }
            }
        }
    }
    repositories {
        maven {
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            def stagingRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : stagingRepoUrl
        }
    }
}
